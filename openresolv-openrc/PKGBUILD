# $Id$
# Maintainer: Chris Cromer <chris@cromer.cl>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ignacio Galmarino <igalmarino@gmail.com>
# Contributor: Aaditya Bagga <aaditya_gnulinux@zoho.com>

pkgname=openresolv-openrc
_pkgname=openresolv
pkgver=3.8.1
pkgrel=1
pkgdesc="resolv.conf management framework (resolvconf)"
arch=(any)
url="http://roy.marples.name/projects/openresolv"
license=('custom:BSD')
backup=(etc/resolvconf.conf)
depends=(bash openrc)
provides=(openresolv)
conflicts=(openresolv)
source=("http://roy.marples.name/downloads/openresolv/${_pkgname}-${pkgver}.tar.xz"
	openrc-path.patch)
sha1sums=('fe4bf41e3ef0323da899cf522e3e5451b3e5c214'
          '7be0d0b055639bf64f618f20e0334015224ba143')
prepare() {
  cd $_pkgname-$pkgver
  # For openrc path
  patch -Np1 -i ../openrc-path.patch
  sed -n '2,25{s:^# \?::;p}' resolvconf.in >LICENSE
}

build() {
  cd $_pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/resolvconf --sbindir=/usr/bin
}

package() {
  cd $_pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$_pkgname/LICENSE"
}
