# Maintainer: artoo <artoo@manjaro.org>
# Contributor: williamh <williamh@gentoo.org>

_url="https://github.com/OpenRC/openrc/archive"

pkgname=openrc
pkgver=0.21
pkgrel=1
pkgdesc="Gentoo's universal init system"
arch=('i686' 'x86_64')
url="http://www.gentoo.org/proj/en/base/openrc/"
license=('BSD2')
depends=('psmisc' 'sysvinit')
optdepends=('netifrc: Network Interface Management Scripts'
            'networkmanager-openrc: NetworkManager'
            'connman-openrc: connman')
conflicts=('systemd-sysvcompat' 'openrc-core')
replaces=('openrc-core')
install=${pkgname}.install
backup=('etc/rc.conf'
        'etc/conf.d/consolefont'
        'etc/conf.d/keymaps'
        'etc/conf.d/hostname'
        'etc/conf.d/modules'
        'etc/conf.d/hwclock'
        'etc/inittab')
#options=('emptydirs')
source=("${pkgname}-${pkgver}.tar.gz::${_url}/${pkgver}.tar.gz"
	"${pkgname}.logrotate"
	'use-optional-modules-load-d.patch'
	'openrc-install.hook'
	'openrc-remove.hook')
sha256sums=('65e83e8b5c2f4316a827e49f4c3da7d98cf02f62b37d6a15d636bb8b9cf5a6e4'
            '0b44210db9770588bd491cd6c0ac9412d99124c6be4c9d3f7d31ec8746072f5c'
            'b8b750c863199e9c039e874c3c79c72d468f35442750156dfbdf503a27977cf3'
            '50bb30157a1669a32cb42b11737ad14a199844cb4469f80bdff6a2d5cde4c544'
            'cbb4d00262a55a508114cb31e3903350be2cff748a4fb5e120dc745292e3987e')

_args=(SYSCONFDIR=/etc)
_args+=(PREFIX=/usr)
_args+=(SBINDIR=/usr/bin)
_args+=(LIBEXECDIR=/usr/lib/rc)
_args+=(MKSELINUX=no)
_args+=(MKPAM=pam)
_args+=(MKTERMCAP=ncurses)
_args+=(MKNET=no)
if [ -f /etc/os-release ]; then
	. /etc/os-release
	_args+=(BRANDING="${NAME}")
else
	_args+=(BRANDING='Unknown Linux')
fi

prepare(){
	cd "${srcdir}/${pkgname}-${pkgver}"
	sed -e "s|/sbin|/usr/bin|g" -i support/sysvinit/inittab
	sed -i 's:0444:0644:' mk/sys.mk
	patch -Np1 -i ${srcdir}/use-optional-modules-load-d.patch
}

build(){
	cd "${srcdir}/${pkgname}-${pkgver}"
	make "${_args[@]}"
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" "${_args[@]}" install

	install -m644 "${srcdir}/${pkgname}-${pkgver}/support/sysvinit/inittab" "${pkgdir}/etc/inittab"
	install -Dm644 "${srcdir}/${pkgname}.logrotate" "${pkgdir}/etc/logrotate.d/${pkgname}"

	sed -e 's/#unicode="NO"/unicode="YES"/' \
	    -e 's/#rc_logger="YES"/rc_logger="YES"/' \
	    -i "${pkgdir}/etc/rc.conf"

	install -d ${pkgdir}/usr/lib/rc/cache

	install -Dm644 ${srcdir}/${pkgname}-${pkgver}/LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"

	for f in openrc-{install,remove}.hook;do
		install -Dm644 ${srcdir}/$f ${pkgdir}/usr/share/libalpm/hooks/$f
	done
}
