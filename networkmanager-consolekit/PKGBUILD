# Maintainer: artoo <artoo@manjaro.org>
# Contributor: Alexey D. <lq07829icatm@rambler.ru>
# Contributor: DaZ <daz.root+arch@gmail.coM>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinxu.org>
# Contributor: Wael Nasreddine <gandalf@siemens-mobiles.org>
# Contributor: Tor Krill <tor@krill.nu>
# Contributor: Will Rea <sillywilly@gmail.com>
# Contributor: Valentine Sinitsyn <e_val@inbox.ru>

pkgname=networkmanager-consolekit
_pkgname=NetworkManager
pkgver=1.0.6
pkgrel=1
_pppver=2.4.7
pkgdesc="NetworkManager with ConsoleKit support for non-systemd systems"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL2.1')
url="http://www.gnome.org/projects/$_pkgname/"
depends=('libnm-glib' 'iproute2' 'libnl' 'polkit-consolekit' 'wpa_supplicant' 'dhclient' 'libsoup'
		'libmm-glib' 'libnewt' 'libndp' 'libteam' 'libgudev' 'consolekit')
optdepends=('dnsmasq: connection sharing'
			'bluez: Bluetooth support'
			'openresolv: resolvconf support'
			'ppp: dialup connection support'
			'rp-pppoe: ADSL support'
			'modemmanager: cellular network support')
makedepends=('intltool' 'dhclient' 'iptables' 'gobject-introspection' 'gtk-doc' "ppp=$_pppver"
             'modemmanager' 'dbus-glib' 'iproute2' 'libnl' 'nss' 'polkit' 'wpa_supplicant' 'libsoup'
             'udev' 'libgudev' 'libmm-glib' 'rp-pppoe' 'libnewt' 'libndp' 'libteam' 'vala')
provides=("networkmanager=$pkgver")
conflicts=('networkmanager')
backup=('etc/NetworkManager/NetworkManager.conf')
install=networkmanager.install
source=("http://ftp.gnome.org/pub/gnome/sources/$_pkgname/${pkgver:0:3}/$_pkgname-$pkgver.tar.xz"
        'disable_set_hostname.patch'
        'NetworkManager.conf'
        '01-org.freedesktop.ModemManager1.rules'
        '01-org.freedesktop.NetworkManager.settings.modify.system.rules'
        '50-org.freedesktop.NetworkManager.rules')
sha256sums=('38ea002403e3b884ffa9aae25aea431d2a8420f81f4919761c83fb92648254bd'
            '25056837ea92e559f09563ed817e3e0cd9333be861b8914e45f62ceaae2e0460'
            '759db295ddae7a6dc6b29211fc0ec08695f875584d456dd146d3679e2c33e2e3'
            '17b5e3ead960a0d2c3cea3f1549d1e83ada9600f174ab9a96f083abe6d8f962d'
            '4b815f43de58379e68653d890f529485aec4d2f83f11d050b08b31489d2267c2'
            '02d9f7d836d297d6ddf39482d86a8573b3e41735b408aa2cd6df22048ec5f6c4')

prepare() {
  cd $_pkgname-$pkgver

  patch -Np1 -i ../disable_set_hostname.patch
  NOCONFIGURE=1 ./autogen.sh
}

build() {
	cd $_pkgname-$pkgver

	AUTOPOINT="intltoolize -f -c --automake" autoreconf -fi
	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--sbindir=/usr/bin \
		--libexecdir=/usr/lib/networkmanager \
		--with-crypto=nss \
		--with-dhclient=/usr/bin/dhclient \
		--without-dhcpcd \
		--with-dnsmasq=/usr/bin/dnsmasq \
		--with-iptables=/usr/bin/iptables \
		--with-systemdsystemunitdir=/usr/lib/systemd/system \
		--with-udev-dir=/usr/lib/udev \
		--with-resolvconf=/usr/bin/resolvconf \
		--with-pppd=/usr/bin/pppd \
		--with-pppd-plugin-dir=/usr/lib/pppd/$_pppver \
		--with-pppoe=/usr/bin/pppoe \
		--with-kernel-firmware-dir=/usr/lib/firmware \
		--with-session-tracking=ck \
		--with-modem-manager-1 \
		--disable-static \
		--enable-more-warnings=no \
		--disable-wimax \
		--enable-modify-system \
		--enable-doc

	make
}

package() {
	cd $_pkgname-$pkgver
	make DESTDIR="${pkgdir}" install
	make DESTDIR="$pkgdir" -C libnm uninstall
	make DESTDIR="$pkgdir" -C libnm-glib uninstall
	make DESTDIR="$pkgdir" -C libnm-util uninstall
	make DESTDIR="$pkgdir" -C vapi uninstall

	install -m644 $srcdir/NetworkManager.conf "$pkgdir/etc/NetworkManager/"

	rm -rf "$pkgdir/usr/include"
	rm -rf "$pkgdir/usr/lib/pkgconfig"
	rm -r "${pkgdir}/var/run"

	install -dm 700 -o polkitd "${pkgdir}"/usr/share/polkit-1/rules.d
	install -m 644 ${srcdir}/01-org.freedesktop.ModemManager1.rules $pkgdir/usr/share/polkit-1/rules.d/
	install -m 644 ${srcdir}/01-org.freedesktop.NetworkManager.settings.modify.system.rules $pkgdir/usr/share/polkit-1/rules.d/

	install -m 644 ${srcdir}/50-org.freedesktop.NetworkManager.rules $pkgdir/usr/share/polkit-1/rules.d/
}
