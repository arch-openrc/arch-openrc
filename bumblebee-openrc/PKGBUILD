# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Maintainer: Roland Singer <roland@manjaro.org>
# Maintainer: Philip Mueller <philm@manjaro.org>

_pkgname=bumblebee
_url=https://raw.githubusercontent.com/gentoo/gentoo/master

pkgname=bumblebee-openrc
pkgver=3.2.1
pkgrel=8
pkgdesc="NVIDIA Optimus support for Linux through Primus/VirtualGL"
arch=('i686' 'x86_64')
provides=('bumblebee')
depends=('primus' 'glib2' 'mesa-libgl' 'openrc')
makedepends=('help2man' 'libbsd')
optdepends=('xf86-video-nouveau: nouveau driver'
            'nouveau-dri: 3D acceleration features fo nouveau'
            'bbswitch: switch on/off discrete card'
            'nvidia: NVIDIA kernel driver'
            'virtualgl: alternative back-end for optirun')
if [ "$CARCH" = "x86_64" ]; then
     optdepends[${#optdepends[@]}]='lib32-primus: faster 32bit back-end for optirun'
     optdepends[${#optdepends[@]}]='lib32-virtualgl: run 32bit applications with optirun'
fi
conflicts=('bumblebee' 'bumblebee-pid')
url="http://www.bumblebee-project.org"
license=("GPL3")
backup=('etc/bumblebee/bumblebee.conf'
        'etc/bumblebee/xorg.conf.nouveau'
        'etc/bumblebee/xorg.conf.nvidia'
        'etc/conf.d/bumblebee')
source=("http://www.bumblebee-project.org/${_pkgname}-${pkgver}.tar.gz"
	"0001-bb_nvidia_modeset-detection_bug699_01.patch::https://github.com/arafey/Bumblebee/commit/5636b24fa86a005a5d2e30bd794516db13ccba56.patch"
        "0002-bb_nvidia_modeset-detection_bug699_02.patch::https://github.com/arafey/Bumblebee/commit/09d537e8e5313cd0f2c7bf6620ca70454de8a04a.patch"
        "0003-bb_nvidia_umv_detection_bug699.patch::https://github.com/arafey/Bumblebee/commit/dbbf20a38aa2bffb10c4e8af583b34dff6bfe721.patch"
        "0004-bb_nvidia_drm_detection_bug699_01.patch::https://github.com/arafey/Bumblebee/commit/bcfe4dd16dd6194f1edbdc53b874a4f408343c5c.patch"
        "0005-bb_nvidia_drm_detection_bug699_02.patch::https://github.com/arafey/Bumblebee/commit/fcfe596eb13f62ca9dd7de272a5a87ae843b2a00.patch"
        "0006-bb_hexadicimal_bug573.patch::https://github.com/Bumblebee-Project/Bumblebee/commit/2073f8537412aa47755eb6f3f22a114403e5285b.patch"
        "0007-bb_mutebblogger.patch"
        "bumblebee.conf"
        "bumblebee.confd::${_url}/x11-misc/bumblebee/files/bumblebee.confd"
        "bumblebee.initd::${_url}/x11-misc/bumblebee/files/bumblebee.initd")
sha256sums=('1018703b07e2f607a4641249d69478ce076ae5a1e9dd6cff5694d394fa7ee30e'
            'aff3528d17a77ff19b4e0a7a10682b8351456f11795f71ef62b315e774fb408a'
            '70ad9b3d8e0d70a504110651c6f5f3a1b1d3c4c44eeb0fd49a4463e99124a47b'
            '16fd522f412125b3c9b5709d78584744c70cb627e8baf8cd6025a71d278f79a6'
            '4676606012319ce7a9e3890d6e83445ae674f5ff2994925d59ec2229fbae5a14'
            '206f71c022b29a77227a3c7ba1f5cf68e219b957b5d807be9a30b6a6a7cafddc'
            '0b7c1f4bb2e27d131c6c21fd7006d075584917ac4259bd9899e6eca99efc0ece'
            'cbe3e1717bc80146b87d8f2ab1158ee9e094ea5bb2ca9a4a8c09c24b086a7792'
            '1c3d4f5d40245a23a5f1cb1f2f6bd4274ff3c5b3749f76a09255191328ae3193'
            '9e0287f542cff0fd3452a0c3d6d427a3404706c93ac9a00f4d99fe52ed596d75'
            'd463b291ec10a40c526fbb6bdfd689865400237cf1e6385edd180a2c9caffcce')

_inst_initd(){
	install -Dm755 ${srcdir}/$1.initd ${pkgdir}/etc/init.d/$1

	sed -e 's|#!/sbin/runscript|#!/usr/bin/openrc-run|' \
		-e 's|/var/run|/run|g' \
		-e 's|/usr/sbin|/usr/bin|g' \
		-i ${pkgdir}/etc/init.d/$1
}

_inst_confd(){
	install -Dm755 ${srcdir}/$1.confd ${pkgdir}/etc/conf.d/$1
}

prepare(){
	cd "${srcdir}/${_pkgname}-${pkgver}"
	for p in ${srcdir}/*.patch; do
		patch -Np1 -i "$p"
	done
}

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	./configure \
		CONF_DRIVER_MODULE_NVIDIA=nvidia \
		CONF_LDPATH_NVIDIA=/usr/lib/nvidia:/usr/lib32/nvidia \
		CONF_MODPATH_NVIDIA=/usr/lib/nvidia/xorg/,/usr/lib/xorg/modules \
		--prefix=/usr \
		--sbindir=/usr/bin \
		--with-udev-rules=/usr/lib/udev/rules.d/ \
		--sysconfdir=/etc \
		--with-pidfile

	make
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"

	make install DESTDIR="$pkgdir"

	_inst_confd 'bumblebee'
	_inst_initd 'bumblebee'

	install -Dm644 "${srcdir}/bumblebee.conf" "${pkgdir}/usr/lib/modprobe.d/bumblebee.conf"
	# Make bash_completion work
	mv -v "${pkgdir}/etc/bash_completion.d/bumblebee" "${pkgdir}/etc/bash_completion.d/optirun"
}
