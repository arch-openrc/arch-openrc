# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Maintainer: Roland Singer <roland@manjaro.org>
# Maintainer: Philip Mueller <philm@manjaro.org>

_pkgname=bumblebee
_url=https://raw.githubusercontent.com/gentoo/gentoo/master

pkgname=bumblebee-openrc
pkgver=3.2.1
pkgrel=8
pkgdesc="NVIDIA Optimus support for Linux through Primus/VirtualGL"
arch=('i686' 'x86_64')
provides=('bumblebee')
depends=('primus' 'glib2' 'mesa-libgl' 'openrc')
makedepends=('help2man' 'libbsd')
optdepends=('xf86-video-nouveau: nouveau driver'
            'nouveau-dri: 3D acceleration features fo nouveau'
            'bbswitch: switch on/off discrete card'
            'nvidia: NVIDIA kernel driver'
            'virtualgl: alternative back-end for optirun')
if [ "$CARCH" = "x86_64" ]; then
     optdepends[${#optdepends[@]}]='lib32-primus: faster 32bit back-end for optirun'
     optdepends[${#optdepends[@]}]='lib32-virtualgl: run 32bit applications with optirun'
fi
conflicts=('bumblebee' 'bumblebee-pid')
url="http://www.bumblebee-project.org"
license=("GPL3")
backup=('etc/bumblebee/bumblebee.conf'
        'etc/bumblebee/xorg.conf.nouveau'
        'etc/bumblebee/xorg.conf.nvidia'
        'etc/conf.d/bumblebee')
source=("http://www.bumblebee-project.org/${_pkgname}-${pkgver}.tar.gz"
	"bumblebee-hexadecimal.patch::https://github.com/Bumblebee-Project/Bumblebee/commit/2073f8537412aa47755eb6f3f22a114403e5285b.patch"
        "bumblebee.confd::${_url}/x11-misc/bumblebee/files/bumblebee.confd"
        "bumblebee.initd::${_url}/x11-misc/bumblebee/files/bumblebee.initd")
sha256sums=('1018703b07e2f607a4641249d69478ce076ae5a1e9dd6cff5694d394fa7ee30e'
            '0b7c1f4bb2e27d131c6c21fd7006d075584917ac4259bd9899e6eca99efc0ece'
            '9e0287f542cff0fd3452a0c3d6d427a3404706c93ac9a00f4d99fe52ed596d75'
            'c1123d391820597cc449839b0254045923bb463e4be605e29bc4b8a06771c6e4')

_inst_initd(){
	install -Dm755 ${srcdir}/$1.initd ${pkgdir}/etc/init.d/$1

	sed -e 's|#!/sbin/runscript|#!/usr/bin/openrc-run|' \
		-e 's|/var/run|/run|g' \
		-e 's|/usr/sbin|/usr/bin|g' \
		-i ${pkgdir}/etc/init.d/$1
}

_inst_confd(){
	install -Dm755 ${srcdir}/$1.confd ${pkgdir}/etc/conf.d/$1
}

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	patch -Np1 < ${srcdir}/bumblebee-hexadecimal.patch
	./configure \
		CONF_DRIVER_MODULE_NVIDIA=nvidia \
		CONF_LDPATH_NVIDIA=/usr/lib/nvidia:/usr/lib32/nvidia \
		CONF_MODPATH_NVIDIA=/usr/lib/nvidia/xorg/,/usr/lib/xorg/modules \
		--prefix=/usr \
		--sbindir=/usr/bin \
		--with-udev-rules=/usr/lib/udev/rules.d/ \
		--sysconfdir=/etc \
		--with-pidfile

	make
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"

	make install DESTDIR="$pkgdir"

	_inst_confd 'bumblebee'
	_inst_initd 'bumblebee'

	# Make bash_completion work
	mv -v "${pkgdir}/etc/bash_completion.d/bumblebee" "${pkgdir}/etc/bash_completion.d/optirun"
}
