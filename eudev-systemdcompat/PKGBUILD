# Maintainer: artoo <artoo@manjaro.org>
# Contributor: Philip MÃ¼ller <philm|manjaro|org>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>

_spkgrel=3
_repo=http://mirror.netcologne.de/archlinux/core/os

pkgname=eudev-systemdcompat
pkgver=229
pkgrel=1
pkgdesc="systemd client libraries without udev; systemd compatibility package"
arch=('i686' 'x86_64')
license=('GPL2')
#groups=('eudev-base')
url="http://www.freedesktop.org/wiki/Software/systemd"
provides=("libsystemd=${pkgver}"
            "systemd=${pkgver}"
            "systemd-tools=${pkgver}"
            'libsystemd.so')
depends=('glib2' 'glibc' 'libgcrypt' 'xz' 'eudev' 'lz4')
conflicts=('systemd'
            'systemd-tools'
            'libsystemd'
            'openrc-systemdcompat')
replaces=('openrc-systemdcompat')

source_i686=("$_repo/i686/libsystemd-$pkgver-${_spkgrel}-i686.pkg.tar.xz"
		"$_repo/i686/systemd-$pkgver-${_spkgrel}-i686.pkg.tar.xz")
sha256sums_i686=('3d8916501b23a37cabc75b238d0314f20627f8da6a685420e8a8c619d82a7a10'
                 '6795477a2a56c192eb63f152ab75e6c52ffa3a20ac83376366086a5b1924bb77')

source_x86_64=("$_repo/x86_64/libsystemd-$pkgver-${_spkgrel}-x86_64.pkg.tar.xz"
		"$_repo/x86_64/systemd-$pkgver-${_spkgrel}-x86_64.pkg.tar.xz"
		"gummibootx64.efi")
sha256sums_x86_64=('27c5c5c9cef7ba9826b3e9f57a649c3ff2d432a75cfbf8f4ce60a68a8e71bf54'
                   '7e6a495e57eb0e2efe4404562192edbb4fc86e74949ffa7f20e53b3beae60fd6'
                   'e0c6a40c74dc3a597dda977cb44bdf21759b8869e152d898d35664b8d3675fd3')



prepare() {
	#clean libsystemd
	rm usr/lib/libudev*.so*

	# clean systemd
	rm -r {etc,var,usr/{include,share}}
	rm usr/lib/libnss*
	for d in usr/lib/*;do
            [[ -d $d ]] && rm -r $d
	done
	for f in usr/bin/*;do
		[[ $f != 'usr/bin/systemd-tmpfiles' && $f != 'usr/bin/systemd-sysusers' ]] && rm $f
	done
}

package() {
	mv "$srcdir/usr/" "$pkgdir"
	if [[ $CARCH == 'x86_64' ]];then
            install -D $srcdir/gummibootx64.efi $pkgdir/usr/lib/systemd/boot/efi/systemd-bootx64.efi
	fi
}
