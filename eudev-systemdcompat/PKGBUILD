# Maintainer: artoo <artoo@manjaro.org>
# Contributor: Philip MÃ¼ller <philm|manjaro|org>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>

_spkgrel=4
_repo=http://mirror.netcologne.de/archlinux/core/os

pkgname=eudev-systemdcompat
pkgver=230
pkgrel=2
pkgdesc="systemd client libraries without udev; systemd compatibility package"
arch=('i686' 'x86_64')
license=('GPL2')
#groups=('eudev-base')
url="http://www.freedesktop.org/wiki/Software/systemd"
provides=("systemd=${pkgver}" "libsystemd=${pkgver}" "systemd-tools=${pkgver}" 'libsystemd.so')
depends=('glib2' 'glibc' 'libgcrypt' 'xz' 'eudev' 'lz4')
conflicts=('systemd-tools' 'systemd' 'libsystemd' 'openrc-systemdcompat')
replaces=('openrc-systemdcompat')

source_i686=("$_repo/i686/libsystemd-$pkgver-${_spkgrel}-i686.pkg.tar.xz"
		"$_repo/i686/systemd-$pkgver-${_spkgrel}-i686.pkg.tar.xz")
source_x86_64=("$_repo/x86_64/libsystemd-$pkgver-${_spkgrel}-x86_64.pkg.tar.xz"
		"$_repo/x86_64/systemd-$pkgver-${_spkgrel}-x86_64.pkg.tar.xz"
		"gummibootx64.efi")
sha256sums_i686=('3c18836df48ec67edeebd774337c13b62ed6e2bb11be62908fd0294e1c9a9d85'
                 '67fe9efb4c9eb18398bc40a663000bbf8688706c247eaf3e012da093790715ef')
sha256sums_x86_64=('7d6d96c6d35a0507f5a3e31001524e74b12e3c3825be0f13d6d4eb9c1cb7b8a4'
                   '2547f64fb0ad1fb31f7289d9239b5a52605ba987a9e70aab9e4640f67e6e46de'
                   'e0c6a40c74dc3a597dda977cb44bdf21759b8869e152d898d35664b8d3675fd3')





prepare() {
	#clean libsystemd
	rm usr/lib/libudev*.so*

	# clean systemd
	rm -r {etc,var,usr/{include,share}}
	rm usr/lib/libnss*
	for d in usr/lib/*;do
            [[ -d $d ]] && rm -r $d
	done
	for f in usr/bin/*;do
		[[ $f != 'usr/bin/systemd-tmpfiles' && $f != 'usr/bin/systemd-sysusers' ]] && rm $f
	done
}

package() {
	mv "$srcdir/usr/" "$pkgdir"
	if [[ $CARCH == 'x86_64' ]];then
            install -D $srcdir/gummibootx64.efi $pkgdir/usr/lib/systemd/boot/efi/systemd-bootx64.efi
	fi
}
