# Maintainer: artoo <artoo@manjaro.org>

_url="https://raw.githubusercontent.com/gentoo/gentoo/master"

pkgname=('brltty-openrc'
        'espeakup-openrc')
pkgver=20160915
pkgrel=1
pkgdesc="OpenRC init scripts"
arch=('any')
url="https://github.com/manjaro/packages-openrc"
license=('GPL2')
conflicts=('systemd-sysvcompat')
source=("brltty.initd::${_url}/app-accessibility/brltty/files/brltty.rc"
        "espeakup.confd::${_url}/app-accessibility/espeakup/files/espeakup.confd"
        "espeakup.initd::${_url}/app-accessibility/espeakup/files/espeakup.rc")
sha256sums=('df59d6b7b884207c09a098188b8418944581c47ed19a1fac9725f847e4281e7f'
            '32e6de11417ebb199a7bf46eb8cf77054b1af1c9f4bcc80b856b34758830eb9f'
            'ac7ea45ed050ba60b618e60c76b3c8c79496f7b837a34246ed328230cf5934cf')

pkgver() {
	date +%Y%m%d
}

_inst_initd(){
	install -Dm755 ${srcdir}/$1.initd ${pkgdir}/etc/init.d/$1

	sed -e 's|#!/sbin/runscript|#!/usr/bin/openrc-run|' \
            -e 's|#!/sbin/openrc-run|#!/usr/bin/openrc-run|' \
		-e 's|/var/run|/run|g' \
		-e 's|/usr/sbin|/usr/bin|g' \
		-i ${pkgdir}/etc/init.d/$1
}

_inst_confd(){
	install -Dm755 ${srcdir}/$1.confd ${pkgdir}/etc/conf.d/$1
}

package_espeakup-openrc() {
	pkgdesc="OpenRC espeakup init script"
	depends=('openrc' 'espeakup')
	optdepends=('alsa-utils-openrc: alsa support')
	backup=('etc/conf.d/espeakup')

	_inst_confd 'espeakup'
	_inst_initd 'espeakup'
}

package_brltty-openrc() {
	pkgdesc="OpenRC brltty init script"
	depends=('openrc' 'brltty')

	_inst_initd 'brltty'

	sed -e 's|/bin/brltty|/usr/bin/brltty|' \
		-i "${pkgdir}/etc/init.d/brltty"
}
