# Maintainer: artoo <artoo@manjaro.org>
# Contributor: williamh <williamh@gentoo.org>

_url="https://github.com"

#_net_url="${_url}/gentoo/netifrc/archive"
_net_url='http://dev.gentoo.org/~robbat2/distfiles'
_func_url="${_url}/gentoo/gentoo-functions/archive"

_func_ver=0.10

pkgname=netifrc
pkgver=0.4.0
pkgrel=1
pkgdesc="Gentoo Network Interface Management Scripts"
arch=('i686' 'x86_64')
url="http://www.gentoo.org/proj/en/base/openrc/"
license=('BSD2')
depends=('udev-openrc')
install=${pkgname}.install
backup=('etc/conf.d/net')
# source=("${pkgname}-${pkgver}.tar.gz::${_net_url}/${pkgver}.tar.gz"
# 	"gentoo-functions-${_func_ver}.tar.gz::${_func_url}/${_func_ver}.tar.gz")
source=("${_net_url}/${pkgname}-${pkgver}.tar.bz2"
	"gentoo-functions-${_func_ver}.tar.gz::${_func_url}/${_func_ver}.tar.gz")
sha256sums=('cc5c10d567530f64ca8c4fb8dcfc8218d102144f83393e1db769a5a5374c00df'
            '709c8b22f404001a512e47a7a4d3192070b3e150fb9d0f943de09736d665b0db')

if [ -f /etc/os-release ]; then
    . /etc/os-release
    _id=$ID
else
    _id=arch
fi
_net_args=(SYSCONFDIR=/etc)
_net_args+=(PREFIX=/usr)
_net_args+=(SBINDIR=/usr/bin)
_net_args+=(LIBEXECDIR=/usr/lib/${pkgname})

_func_args+=(ROOTPREFIX=/usr)
_func_args+=(ROOTSBINDIR=/usr/bin)
_func_args+=(ROOTLIBEXECDIR=/usr/lib/$_id)

build(){
	# make netifrc
	cd "${srcdir}/${pkgname}-${pkgver}"
	make "${_net_args[@]}"
	cd ${srcdir}/gentoo-functions-${_func_ver}
	make "${_func_args[@]}"
}

package() {
        cd "${srcdir}/${pkgname}-${pkgver}"
        make DESTDIR="${pkgdir}" "${_net_args[@]}" install
        install -Dm 644 "${srcdir}/${pkgname}-${pkgver}/doc/net.example" "${pkgdir}/etc/conf.d/net"

        install -d "${pkgdir}/etc/runlevels/boot"
        ln -sf "/etc/init.d/net.lo" "${pkgdir}/etc/runlevels/boot/net.lo"

        cd ${srcdir}/gentoo-functions-${_func_ver}
        make DESTDIR="${pkgdir}" "${_func_args[@]}" install

        sed -e "s|/lib/gentoo/functions.sh|/usr/lib/$_id/functions.sh|g" \
		-i "${pkgdir}/usr/lib/${pkgname}/sh/functions.sh"

        install -Dm644 ${srcdir}/${pkgname}-${pkgver}/LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
