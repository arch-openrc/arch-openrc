# Maintainer: artoo <artoo@manjaro.org>
# Contributor: kozec <kozec at kozec.com>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgname=polkit

pkgname=polkit-consolekit
pkgver=0.113+29+g3272a98
pkgrel=4
pkgdesc="PolyciKit with ConsoleKit support for non-systemd systems"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://www.freedesktop.org/wiki/Software/polkit"
depends=('glib2' 'pam' 'expat' 'js' 'udev')
makedepends=('intltool' 'gtk-doc' 'gobject-introspection' 'git' 'autoconf-archive')
provides=("polkit=$pkgver")
conflicts=('polkit')
options=('!libtool')
install=polkit.install
_commit=3272a988655c3236b55bad70e9a3af20857f384b
#source=("http://www.freedesktop.org/software/polkit/releases/$_pkgname-$pkgver.tar.gz")
source=("git+https://anongit.freedesktop.org/git/polkit#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
    cd $_pkgname
    git describe --tags | sed 's/-/+/g'
}

prepare() {
    cd $_pkgname
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd $_pkgname #-$pkgver

    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --sbindir=/usr/bin \
        --localstatedir=/var \
        --libexecdir=/usr/lib/polkit-1 \
        --enable-libsystemd-login=no \
        --disable-static \
        --enable-gtk-doc \
        --with-os-type=redhat
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
	make
}

check() {
    cd $_pkgname #-$pkgver
    make -k check || :
}

package() {
    cd $_pkgname #-$pkgver
    make DESTDIR="$pkgdir" install

    chown root:102 "$pkgdir"/{etc,usr/share}/polkit-1/rules.d
    chmod 750      "$pkgdir"/{etc,usr/share}/polkit-1/rules.d
}
