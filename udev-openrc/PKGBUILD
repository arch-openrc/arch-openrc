# Maintainer: artoo <artoo@manjaro.org>

_url="http://dev.gentoo.org/~williamh/dist"
_pkgname=udev-init-scripts

pkgname=udev-openrc
pkgver=30
pkgrel=4
pkgdesc="OpenRC udev startup scripts"
arch=('any')
url="https://github.com/manjaro/packages-openrc"
license=('GPL2')
groups=('openrc-base' 'openrc-desktop')
depends=('openrc' 'udev')
conflicts=('systemd-sysvcompat')
source=("${_url}/${_pkgname}-${pkgver}.tar.gz"
	"kmod-static-nodes.initd")
sha256sums=('4de998d68e28f8ce4c1d4c9955aec0fa4cfe84673cbf3bfe1be71a1e9918693f'
            '3ecbe7761b92a123a5a3d395ec3d9e090b0dfba7910a5987b4a2298c20a7e571')

package(){
        cd "${srcdir}/${_pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	for f in ${pkgdir}/etc/init.d/*;do
		sed -e "s|#!/sbin/openrc-run|#!/usr/bin/openrc-run|" \
                    -e "s|#!/sbin/runscript|#!/usr/bin/openrc-run|" \
			-e "s|/bin/udevadm|/usr/bin/udevadm|g" \
			-e "s|/sbin/udevd|/usr/bin/udevd|g" \
			-i "$f"
	done
	install -d "${pkgdir}/etc/runlevels/sysinit"

	ln -sf "/etc/init.d/udev" "${pkgdir}/etc/runlevels/sysinit/udev"
	ln -sf "/etc/init.d/udev-trigger" "${pkgdir}/etc/runlevels/sysinit/udev-trigger"

	# kmod-static-nodes
	install -Dm755 ${srcdir}/kmod-static-nodes.initd ${pkgdir}/etc/init.d/kmod-static-nodes

	ln -sf "/etc/init.d/kmod-static-nodes" "${pkgdir}/etc/runlevels/sysinit/kmod-static-nodes"
}
