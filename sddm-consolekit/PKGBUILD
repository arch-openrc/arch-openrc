# Maintainer: artoo <artoo@manjaro.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

_pkgname=sddm

pkgname=sddm-consolekit
pkgver=0.14.0
pkgrel=1
pkgdesc='QML based X11 display manager'
arch=('i686' 'x86_64')
url='http://github.com/sddm/sddm'
license=('GPL')
depends=('qt5-declarative' 'dbus-openrc' 'eudev-systemd' 'consolekit')
makedepends=('extra-cmake-modules' 'python-docutils' 'qt5-tools')
install="${_pkgname}.install"
backup=('usr/share/sddm/scripts/Xsetup'
	'etc/sddm.conf')
conflicts=('display-manager' 'sddm')
provides=('display-manager' 'sddm')
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/${_pkgname}/${_pkgname}/archive/v${pkgver}.tar.gz"
        "${_pkgname}-consolekit.patch"
        "${_pkgname}-pam_kwallet.patch"
        'sddm.conf')
sha256sums=('a551551a6ba324e9c384c89bc63e871de65fea3740eadbea2d63df86045f8205'
            'a998f8a2594cd1e81f402605f01075477a017269b0ac93a22f5faa7d355d18e0'
            'dc46a0132cf0ea722a69cff45ed1ded3c93ade008e22fc90d1b797ca85ed535a'
            'ffcb9885cb41da54a97f62c2538975fbb9dc80c2b58326d479a87ea0f56f02cf')

prepare() {
	mkdir -p build
	cd ${_pkgname}-${pkgver}
	patch -p1 -i ${srcdir}/${_pkgname}-consolekit.patch
	patch -p1 -i ${srcdir}/${_pkgname}-pam_kwallet.patch
}

build() {
	cd build
	cmake ../${_pkgname}-${pkgver} \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_LIBEXECDIR=/usr/lib/sddm \
		-DDBUS_CONFIG_FILENAME=org.freedesktop.sddm.conf \
		-DBUILD_MAN_PAGES=ON
	make
}

package() {
	cd build
	make DESTDIR="${pkgdir}" install
# 	sed -e 's|pam_systemd.so|pam_ck_connector.so nox11|' -i ${pkgdir}/etc/pam.d/sddm-greeter
	install -m 755 ${srcdir}/sddm.conf ${pkgdir}/etc
}
